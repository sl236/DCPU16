; ----- format_hex
; places hex representation of A into four words starting at B, ORing with C
format_hex:
  set push, a 
  set push, x

  set x, a
  and x, 0xF
  set x, [x+format_hex_data]
  bor x, c
  set [b+3], x
  shr a, 4
  set x, a
  and x, 0xF
  set x, [x+format_hex_data]
  bor x, c
  set [b+2], x
  shr a, 4
  set x, a
  and x, 0xF
  set x, [x+format_hex_data]
  bor x, c
  set [b+1], x
  shr a, 4
  set a, [a+format_hex_data]
  bor a, c
  set [b], a
  
  set x, pop  
  set a, pop  
  set pc, pop
  
format_hex_data: dat "0123456789ABCDEF"

; ----- strncpy
; Copies null-terminated string at I to J, or the first A elements of it if the string is longer.
; Strings are assumed to be terminated with an entire 0 word.
; I and J are advanced to the ends of the respective strings.
; Note if the string is longer than A elements, the result is not null terminated.
strncpy:
	ife a, 0
		set pc, pop
	ife [i], 0
		set pc, pop
	set push, a
	add a, i
1:
	sti [j], [i]
	ife [i], 0
		b 1f
	ifn i, a
		b 1b
	set a, pop
	set pc, pop

; ----- strcmp
; Compares null-terminated strings I and J. Returns 0 in A if they match, a signed value suitable for ordering otherwise
; Strings are assumed to be terminated with an entire 0 word, and either both packed or neither packed.
; I and J are advanced to terminating word, or first word that differs.
strcmp:
	ifn [i], [j]		; characters don't match?
		b 1f	; skip to end
	ifn [i], 0
		sti pc, strcmp
1:
	set a, [i]
	sub a, [j]
	set pc, pop

; ----- strcmp_ia
; Compares null-terminated strings I and A. Returns 0 in A if they match, a signed value suitable for ordering otherwise
; I is assumed to be one word per character; A is treated as a packed string.
; I is advanced to terminating null, or first character that differs.
strcmp_ia:
	set push, j			; preserve j
	set push, x			; and x
	set x, a
	set a, [x]
1:
	shr a, 8
	ifn [i], a			; characters don't match?
		b strcmp_ia_done
	ife [i], 0
		b strcmp_ia_done
	sti a, [x]
	and a, 0x7f
	ifn [i], a			; characters don't match?
		b strcmp_ia_done
	ife [i], 0
		b strcmp_ia_done
	add x, 1
	sti a, [x]
	b 1b

strcmp_ia_done:
	sub a, [i]
	set x, pop			; restore regs
	set j, pop
	set pc, pop			; return

; ---- memcpy
; copies c words from memory at j to memory at i
; i and j are advanced by c, other registers preserved
memcpy:
    set push, b     ; preserve b so we can use it for a temporary value
    set push, c     ; save a copy of the count
    and c, 7        ; c is now count % 8
    set b, memcpy_loop+8   ; end of unrolled loop
    sub b, c        ; b now points to correct position in loop
    set c, [sp]     ; restore count
    add c, j        ; c now holds final address
    set pc, b       ; enter loop
memcpy_loop:
    sti [i], [j]
    sti [i], [j]
    sti [i], [j]
    sti [i], [j]    
    sti [i], [j]
    sti [i], [j]
    sti [i], [j]
    sti [i], [j]
    ifn j, c        ; done?
        sub pc, (memcpy_loop_end-memcpy_loop)
memcpy_loop_end:
    set c, pop
    set b, pop
    set pc, pop