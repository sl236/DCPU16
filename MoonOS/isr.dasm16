; ------------------
; interrupt handlers
; ------------------

def isr_header_magic 0x1ceb		; bor j, j - a no-op; this allows us to have trampolines
isr_entry:
	bor a, 0
	ifn [a-1], isr_header_magic		; sanity check: does the message hold the address of an ISR?
	    rfi 0						; nope. Ignore it
	set pc, a						; branch to handler



dat isr_header_magic	; ---- keyboard handler	
isr_handle_keyboard:
	set push, b					; store b and c for interrupted process
	set push, c
	
	set a, 2					; key held?
	set b, 0x91					; CTRL
	hwi [keyboard_device_id]	; ask the keyboard
	set [ctrl_held], c			; store the response
	ifn c, 0					; *is* it held?
		b isr_hk_ctrl			; if so, users don't see our keypresses
	
    set b, [screen_curr_terminal]	; got a foreground terminal?
    ife b, 0						; nope, so can't have a process blocking on it
    	b isr_hk_fastexit
	ife [b+term_waiting_keyproc], 0	; process waiting on keyboard?
		b isr_hk_fastexit			; nope. Bail.
		
	set push, x					; can't trash registers of whatever we interrupted, store them
	set push, y					
	set x, [b+term_waiting_keyproc]	; fetch waiting process
	set [b+term_waiting_keyproc], 0 ; it's not waiting any more
	set y, [proclist]			; active process
	jsr dll_splice				; add the process that was waiting just before it
	set y, pop					; restore registers
	set x, pop
	
isr_hk_fastexit:	
	set c, pop
	set b, pop
	rfi 0						; exit ISR

isr_hk_ctrl:
	set a, 1					; poll key queue
	hwi [keyboard_device_id]	; do that
	ifn c, 0					; got something?
		set [ctrl_keypress], c	; stash it
	b isr_hk_fastexit			; done



dat isr_header_magic	; ---- clock tick handler	
isr_handle_clock:
	set push, ex
	add [clock_time], 1
	add [clock_time+1], ex
	set ex, pop
	rfi 0

dat isr_header_magic	; ---- HMD2043 handler
isr_handle_hmd2043:
	dat isr_header_magic		; support up to 4 devices
	dat isr_header_magic
	dat isr_header_magic
	dat isr_header_magic
	set push, ex
	set push, b
	set push, x
	set push, y
	sub a, isr_handle_hmd2043	; A now contains device ID 0..3
	set y, a					; stash it
	
	set a, 4								; QUERY_INTERRUPT_TYPE
	hwi [fdd_device_id+y]					; send that
	set pc, [b+isr_handle_hmd2043_handlers]	; select appropriate handler

isr_handle_hmd2043_handlers:
	dat isr_handle_hmd2043_exit				; NONE
	dat isr_handle_hmd2043_media			; MEDIA_STATUS
	dat isr_handle_hmd2043_opcomplete		; READ_COMPLETE
	dat isr_handle_hmd2043_opcomplete		; WRITE_COMPLETE
	
isr_handle_hmd2043_opcomplete:
	set x, [fdd_active_op+y]				; active operation
	ife x, 0								; exists?
		set pc, isr_handle_hmd2043_exit		; nope. look the other way and whistle.
	set [x+io_op_result], a					; store operation result
	set [x+io_op_next], [fdd_completed_ops] ; about to make it new head of completed ops list	
	set [fdd_completed_ops], x				; do that

1:
	set x, [fdd_pending_ops+y]				; pending ops list
	set [fdd_active_op+y], x				; this op now active
	ife x, 0								; empty?
		set pc, isr_handle_hmd2043_exit		; bail
	set [fdd_pending_ops+y], [x+io_op_next]	; its next is head of pending ops list
	set push, c
	jsr [x+io_op_type]
	set c, pop
	ife a, 0								; successfully issued?
		set pc, isr_handle_hmd2043_exit		; we're done here
	set b, a								; result code in b
	set pc, isr_handle_hmd2043_opcomplete	; this op is complete
	
	
isr_handle_hmd2043_media:
	set a, 0								; QUERY_MEDIA_PRESENT
	hwi [fdd_device_id+y]					; send that	
	set [fdd_media_present+y], b			; store result
	; drop through
	
isr_handle_hmd2043_exit:
	set y, pop
	set x, pop
	set b, pop
	set ex, pop
	rfi 0